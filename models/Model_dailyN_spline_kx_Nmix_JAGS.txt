# N mixture gray whale model in jags 
# In this version, I try to replace normal/spline models with 
# GAM. 

# Tomo Eguchi
# 2022-04-21 started


model{ 
  
  # count i
  # station (Trailer) s
  # year t
    
  ### Linking Lambda from N Mixture to GAM
     
  ### N Mixture process
  
  for(t in 1:n.year){
	
    for(j in 1:periods[t]){ 
                            
      for(s in 1:n.station[t]){
      
        # N mixture model - two observations of 'true' N (passing whales), 
		# with different observation probabilities when there are two stations
		# Otherwise, zero counts from the second observation point
		
		 # Error in node n[30,1,1] Node inconsistent with parents 2022-08-19
		 # n[30,1,1] is the first non zero data point. That must be the problem. 
        #n[j,s,t] ~ dbin(obs.prob[j, s, t], N[day[j,t], t]) 
		#n[j,s,t] ~ dpois(obs.prob[j, s, t] * N[day[j,t], t]) 
		n[j,s,t] ~ dnorm(N[day[j,t],t], tau.n)

      }#s    
    }#j
  }#t

  # The Poisson mean is modeled with spline, which is the true number of whales passing through the study area
  # Provide X.j in the data
  for(t in 1:n.year){

    for(d in 1:90){ 

      mu.N[d,t] <- inprod(X.j[d,], mu.N.beta[,t]) 
	  
      #N[d,t] ~ dpois(mu.N[d,t]) 
	  N[d,t] ~ dnorm(mu.N[d,t], tau.N)T(0,) 

    }#d
  }#t
  
  sigma.N ~ dgamma(1, 0.1)
  tau.N <- sigma.N^(-2)

  sigma.n ~ dgamma(1, 0.1)
  tau.n <- sigma.n^(-2)
  
  # Prior on N.beta
  for (t in 1:n.year){
	mu.N.beta[1, t] ~ dnorm(1000, 0.01)
    for (i in 2:N_knots){
      mu.N.beta[i,t] ~ dnorm(mu.N.beta[i-1, t], tau.N.beta)

    }
  }

  ### Observation probability - include the watch period durations
  for(t in 1:n.year){
    for(j in 1:periods[t]){
      for(s in 1:n.station[t]){

        # Final obs prob = observer 1/0 * 
        #                (base obs prob + BF on/off * Fixed effect of BF * BF + 
        #                 VS on/off * Fixed effect of VS * VS    + 
        #                Obs effect on/off * Random effect of observer)

        logit(prob[j,s,t]) <- ifelse(watch.prop[j,t] > 0,
						logit(mean.prob) + (BF.Switch * BF.Fixed * bf[j,t]) + (VS.Switch * VS.Fixed * vs[j,t]) + (OBS.Switch * OBS.RF[obs[j,s,t]]),
						log(0))
       
        # watch.prop is the proportion of the total watch periods in a day (540 minutes) that
        # was actually observed. 
        obs.prob[j,s,t] <- prob[j,s,t] #* watch.prop[j,t]
                
      }#s
    }#j
  }#t
  
  # Flat prior for mean.prob
  mean.prob ~ dunif(0,1)
  
  sigma.N.beta ~ dgamma(5, 0.5)
  tau.N.beta <- sigma.N.beta^(-2)
  
  ### Specification of terms within observation probability linear model
  
  ## Observer random effect
  for(o in 1:n.obs){
    OBS.RF[o] ~ dnorm(0,tau.Obs)
  }#o
  
  sigma.Obs ~ dgamma(1, 0.1)
  tau.Obs <- pow(sigma.Obs,-2)
       
  OBS.Switch ~ dbern(0.5)
  
  ## Beaufort
  #uninformative prior for the BF.Switch, which determines whether to include the effect of beaufort conditions (multiply by 0 or 1)
  BF.Switch ~ dbern(0.5) 
  
  #Below is the single fixed effect multiplied by BF rating in the updated obs prob equation:
  BF.Fixed ~ dnorm(0,0.01)
  
  ## Visibility
  VS.Switch ~ dbern(0.5) #uninformative prior for the VS.Switch
    
  #Below is the single fixed effect multiplied by VS rating in the updated obs prob equation:
  VS.Fixed ~ dnorm(0,0.01)
  
  ### Summaries, Abundance Estimates, and Other Derived Quantities 
  for(t in 1:n.year){
	
    raw.unrounded[t] <- sum(N[1:90,t])
    Raw.Est[t] <- round(raw.unrounded[t])
	
	 # multiply raw estimates by correction factor for nighttime passage rates (below)
    Corrected.Est[t] <- Raw.Est[t] * corr.factor 
  }#t
  

  # Correction factor for nighttime passage rates:
  corr.factor ~ dnorm(mean.corr, tau.corr)
  mean.corr <- 1.0875
  sd.corr <- 0.03625
  tau.corr <- pow(sd.corr,-2)
  
}#model

