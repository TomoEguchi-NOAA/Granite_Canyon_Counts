# N mixture gray whale model in jags 
# In this version, the observation is modeled with binomial distribution
# True N is modeled with negative binomial distribuiton, where the mean of the 
# negative binomial distribution is fitted with spline. 


# Tomo Eguchi
# 2022-08-29 Modified from other spline models


model{ 
  
  # count i
  # station (Trailer) s
  # year t
         
  ### N Mixture process
  # Observation model - binomial 
  for(t in 1:n.year){
	
    for(j in 1:periods[t]){ 
                            
      for(s in 1:n.station[t]){
      
        # N mixture model - two observations of 'true' N (passing whales), 
		# with different observation probabilities when there are two stations
		# Otherwise, zero observation probability from the second observation point
				
        n[j,s,t] ~ dbin(obs.prob[j, s, t], N[day[j,t], t]) 
		# 5 nodes produced errors; first error: Error in node n[30,1,1]
		# Node inconsistent with parents
		
		log.lkhd[j,s,t] <- logdensity.bin(n[j,s,t], obs.prob[j,s,t], N[day[j,t], t])
		
      }#s    
    }#j
  }#t

  # The state mean is modeled with a State-Space model (AR(1)), which is the true 
  # number of whales passing through the study area

  for (t in 1:n.year){
	mu.N[1,t] <- 0   # by assumption 
	N[1,t] <- mu.N[1,t]
  }
  
  for(t in 1:n.year){

    for(d in 2:90){ 

      mu.N[d,t] <- theta[d] * mu.N[d-1,t]
	  N.0[d,t] ~ dnorm(mu.N[d,t], tau.N[t])
	  N[d,t] <- ifelse(N.0[d,t] > 0, round(N.0[d,t]), 0)
	  	  
    }#d
  }#t
  
  sigma.N ~ dgamma(1, 0.1)

  for (d in 1:90){
	theta[d] ~ dnorm(0,0.1)
  }
	  
  for (t in 1:n.year){
	tau.N[t] <- sigma.N^(-2)
  }

  ### Observation probability - include the watch period durations
  for(t in 1:n.year){
    for(j in 1:periods[t]){
      for(s in 1:n.station[t]){

        # Final obs prob = observer 1/0 * 
        #                (base obs prob + BF on/off * Fixed effect of BF * BF + 
        #                 VS on/off * Fixed effect of VS * VS    + 
        #                Obs effect on/off * Random effect of observer)

        logit(prob[j,s,t]) <- logit(mean.prob) + 
								(BF.Switch * BF.Fixed * bf[j,t]) + 
								(VS.Switch * VS.Fixed * vs[j,t]) + 
								(OBS.Switch * OBS.RF[obs[j,s,t]])
						
       
        # watch.prop is the proportion of the total watch periods in a day (540 minutes) that
        # was actually observed. 
        obs.prob[j,s,t] <- ifelse(watch.prop[j,t] > 0,
									watch.prop[j,t] * prob[j,s,t],
									0)
                
      }#s
    }#j
  }#t
  
  # Flat prior for mean.prob
  mean.prob ~ dunif(0,1)
    
  ### Specification of terms within observation probability linear model
  
  ## Observer random effect
  for(o in 1:n.obs){
    OBS.RF[o] ~ dnorm(0,tau.Obs)
  }#o
  
  sigma.Obs ~ dgamma(1, 0.1)
  tau.Obs <- pow(sigma.Obs,-2)
       
  OBS.Switch ~ dbern(0.5)
  
  ## Beaufort
  #uninformative prior for the BF.Switch, which determines whether to include the effect of beaufort conditions (multiply by 0 or 1)
  BF.Switch ~ dbern(0.5) 
  
  #Below is the single fixed effect multiplied by BF rating in the updated obs prob equation:
  BF.Fixed ~ dnorm(0,0.01)
  
  ## Visibility
  VS.Switch ~ dbern(0.5) #uninformative prior for the VS.Switch
    
  #Below is the single fixed effect multiplied by VS rating in the updated obs prob equation:
  VS.Fixed ~ dnorm(0,0.01)
  
  ### Summaries, Abundance Estimates, and Other Derived Quantities 
  for(t in 1:n.year){
	
    raw.unrounded[t] <- sum(N[1:90,t])
    Raw.Est[t] <- round(raw.unrounded[t])
	
	 # multiply raw estimates by correction factor for nighttime passage rates (below)
    Corrected.Est[t] <- Raw.Est[t] * corr.factor 
  }#t
  

  # Correction factor for nighttime passage rates:
  corr.factor ~ dnorm(mean.corr, tau.corr)
  mean.corr <- 1.0875
  sd.corr <- 0.03625
  tau.corr <- pow(sd.corr,-2)
  
}#model

